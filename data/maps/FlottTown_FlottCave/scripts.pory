const DIR_SOUTH =  1
const DIR_NORTH =  2
const DIR_WEST  =  3
const DIR_EAST  =  4

const LOCALID_ZUBAT = 10

const LOCALID_MON0 = 3
const LOCALID_MON1 = 4
const LOCALID_MON2 = 2
const LOCALID_MON3 = 1
const LOCALID_MON4 = 9

const LOCALID_ELSIE = 7
const LOCALID_BARNEY = 8

const PLAYER = OBJ_EVENT_ID_PLAYER
const PLAYERX = VAR_TEMP_0
const PLAYERY = VAR_TEMP_1
const ZUBATX = VAR_TEMP_2
const ZUBATY = VAR_TEMP_3
const PLAYERDIR = VAR_TEMP_4
const STARTERX = VAR_TEMP_5
const STARTERY = VAR_TEMP_6

const MON_NAME = VAR_TEMP_7
const MON_ID = VAR_TEMP_8

mapscripts FlottTown_FlottCave_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: FlottTown_FlottCave_OnTransition
    MAP_SCRIPT_ON_FRAME_TABLE [ 
        VAR_FLOTT_TOWN_STATE, 6: FlottTown_FlottCave_OnFrame
        // VAR_FLOTT_TOWN_STATE, 9: FlottTown_FlottCave_TurnOn
    ]
}

script(local) FlottTown_FlottCave_OnTransition {
    destroyfollower
    setflag(FLAG_BARNEY_IN_CAVE)
    setflag(FLAG_ELSIE_IN_CAVE)
    call(FlottTown_FlottCave_CaveLightLevel)
}

script(local) FlottTown_FlottCave_OnFrame{
    msgbox(FlottTown_FlottCave_Text_ElsieWorried, MSGBOX_DEFAULT)
    closemessage
    setflag(FLAG_FALLEN_INTO_CAVE)
    setvar(VAR_FLOTT_TOWN_STATE, 7)
    end
}

script(local) FlottTown_FlottCave_CaveLightLevel{
    setflashlevel(6)
    end
}

// script FlottTown_FlottCave_TurnOn{
//     setflashlevel(0)
// }

script FlottTown_FlottCave_EventScript_ZubatAttacks{
    lockall
    msgbox(format("As you walk inside the dark cave, you hear a noise coming from the darkness!"), MSGBOX_DEFAULT)
    waitmessage
    closemessage
    playmoncry(SPECIES_ZUBAT, CRY_MODE_NORMAL)
    waitmoncry
    call(FlottTown_FlottCave_ZubatSpawn)
    //special(CheckZubatSpawning)
    applymovement(LOCALID_ZUBAT, Common_Movement_FacePlayer)
    fadescreen(FADE_TO_BLACK)
    msgbox(format("As the Pokémon is about to attack, you feel a presence approach."))
    call(StarterSelection_GFXSetting)
    call(FlottTown_FlottCave_StarterChoice)
    call(FlottTown_FlottCave_WildZubatBattle)
    delay(60)
    call(FlottTown_FlottCave_StarterWantsToJoin)
    clearflag(FLAG_FALLEN_INTO_CAVE)
    setvar(VAR_FLOTT_TOWN_STATE, 8)
    fadescreen(FADE_TO_BLACK)
    setvar(VAR_0x8004, 13) # x
    setvar(VAR_0x8005, 12) # y
    special(TeleportCamera)
    setobjectxy(PLAYER, 13, 12)
    applymovement(PLAYER, Common_Movement_FaceDown)
    msgbox(format("You decide to wait with your new partner for your sister to return."))
    waitmessage
    call(FlottTown_FlottCave_EventScript_RockSmash)
    end
}

script FlottTown_FlottCave_ZubatSpawn{
    getplayerxy(PLAYERX, PLAYERY)
    getobjectfacingdirection(PLAYER, VAR_TEMP_4)
    copyvar(ZUBATX, PLAYERX)
    copyvar(ZUBATY, PLAYERY)
    copyvar(STARTERX, PLAYERX)
    copyvar(STARTERY, PLAYERY)
    switch(var(VAR_TEMP_4)){
        case DIR_NORTH:
            applymovement(PLAYER, Common_Movement_FaceDown)
            addvar(ZUBATY, 2)
            addvar(STARTERY, 1)
        case DIR_SOUTH:
            applymovement(PLAYER, Common_Movement_FaceUp)
            subvar(ZUBATY, 2)
            subvar(STARTERY, 1)
        case DIR_WEST:
            applymovement(PLAYER, Common_Movement_FaceRight)
            addvar(ZUBATX, 2)
            addvar(STARTERX, 1)
        case DIR_EAST:
            applymovement(PLAYER, Common_Movement_FaceLeft)
            subvar(ZUBATX, 2)
            subvar(STARTERX, 1)
    }
    addobject(LOCALID_ZUBAT)
    setobjectxy(LOCALID_ZUBAT, ZUBATX, ZUBATY)
    return
}

script FlottTown_FlottCave_EventScript_RockSmash{
    fadescreen(FADE_FROM_BLACK)
    msgbox(format("???: Golurk! Use Rock Smash!"), MSGBOX_DEFAULT)
    playmoncry(SPECIES_GOLURK, CRY_MODE_NORMAL)
    waitmoncry
    playse(SE_M_ROCK_THROW)
    waitse
    applymovement(PLAYER, Common_Movement_FaceLeft)
    clearflag(FLAG_ELSIE_IN_CAVE)
    clearflag(FLAG_BARNEY_IN_CAVE)
    setflag(FLAG_TEMP_11)
    removeobject(6)
    callnative(RemoveFollowingPokemon)
    waitse
    msgbox(format("That sounded like it came from the smashable rocks!"), MSGBOX_DEFAULT)
    closemessage
    end
}

script FlottTown_FlottCave_RightTrigger{
    applymovement(PLAYER, Common_Movement_WalkLeft)
    call(FlottTown_FlottCave_EventScript_ElsieRescue)
}
script FlottTown_FlottCave_BottomTrigger{
    applymovement(PLAYER, FlottTown_FlottCave_PlayerFacesBarneyFromBottomTrigger)
    call(FlottTown_FlottCave_EventScript_ElsieRescue)
}

script FlottTown_FlottCave_EventScript_ElsieRescue{
    applymovement(PLAYER, Common_Movement_FaceLeft)
    msgbox(FlottTown_FlottCave_Text_Rescue, MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_BARNEY, FlottTown_FlottCave_Movement_BarneyWalksUp)
    applymovement(PLAYER, FlottTown_FlottCave_Movement_PlayerWalksUp)
    applymovement(LOCALID_ELSIE, FlottTown_FlottCave_Movement_ElsieWalksUp)
    waitmovement(LOCALID_BARNEY)
    waitmovement(LOCALID_ELSIE)
    waitmovement(PLAYER)
    applymovement(LOCALID_ELSIE, Common_Movement_FaceRight)
    msgbox(format("ELSIE: {PLAYER}! Thank goodness you're alright. I don't know what I'd do if you got hurt."), MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(LOCALID_ELSIE, Common_Movement_WalkInPlaceRight)
    msgbox(format("ELSIE: This is Mr. Heinrich. This was his old gym."), MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(LOCALID_ELSIE, Common_Movement_FaceRight)
    applymovement(LOCALID_BARNEY, Common_Movement_WalkInPlaceDown)
    msgbox(FlottTown_FlottCave_Text_LetsGoHome, MSGBOX_DEFAULT)
    waitmessage
    closemessage
    setflag(FLAG_ELSIE_IN_CAVE)
    setflag(FLAG_BARNEY_IN_CAVE)
    clearflag(FLAG_BARNEY_IN_HOUSE)
    clearflag(FLAG_ELSIE_IN_BARNEYS_HOUSE)
    setvar(VAR_FLOTT_TOWN_STATE, 9)
    warpsilent(MAP_FLOTT_TOWN_BARNEYS_HOUSE, 7, 4)
    end
}

script(local) FlottTown_FlottCave_StarterChoice{
    lockall
	msgbox(format("But…before it could hurt you, something intervenes.\l What is it?"), MSGBOX_DEFAULT)
    checkplayerpersonality
    switch(var(VAR_RESULT)){
        case 0:
            call(StarterSelection_FuryPersonality)
        case 1:
            call(StarterSelection_MischiefPersonality)
        case 2:
            call(StarterSelection_FriendshipPersonality)
    }
    fadescreen(FADE_FROM_BLACK)
    msgbox("{STR_VAR_1} jumped in to save you!", MSGBOX_DEFAULT)
    playmoncry(MON_NAME, CRY_MODE_NORMAL)
    waitmoncry
    givemon(MON_NAME, 10, ITEM_NONE)
    return
}

script(local) FlottTown_FlottCave_WildZubatBattle{
    removeitem(ITEM_POKE_BALL, 1)
    setwildbattle(SPECIES_ZUBAT, 10)
    dowildbattle
    fadescreen(FADE_TO_BLACK)
    removeobject(LOCALID_ZUBAT)
    fadescreen(FADE_FROM_BLACK)
    msgbox(format("The wild Zubat ran away!"), MSGBOX_DEFAULT)
    closemessage
    return
}

script(local) FlottTown_FlottCave_StarterWantsToJoin{
    applymovement(MON_ID, Common_Movement_FacePlayer)
    msgbox("It looks like the {STR_VAR_1} wants\nto join you. Let it?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        playfanfare(MUS_OBTAIN_ITEM)
        msgbox(format("{STR_VAR_1} joined you!"), MSGBOX_DEFAULT)
        waitfanfare
        setflag(FLAG_SYS_POKEMON_GET)
        removeobject(MON_ID)
        msgbox("Give a nickname?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES){
            call(Common_EventScript_GetGiftMonPartySlot)
            call(Common_EventScript_NameReceivedPartyMon)
        }
        else{closemessage}
        return      
    }
    else{
        msgbox("The Pokémon looks sad.", MSGBOX_DEFAULT)
        call(FlottTown_FlottCave_StarterWantsToJoin)
    }
}

movement FlottTown_FlottCave_PlayerFacesBarneyFromBottomTrigger{
    walk_up
    face_left
    step_end
}


movement FlottTown_FlottCave_Movement_BarneyWalksUp{
    walk_up * 4
    face_down
    step_end
}

movement FlottTown_FlottCave_Movement_ElsieWalksUp{
    walk_up * 3
    step_end
}

movement FlottTown_FlottCave_Movement_PlayerWalksUp{
    walk_left
    walk_up * 3
    step_end
}

text(local) FlottTown_FlottCave_Text_ElsieWorried{
    format("ELSIE: Oh my god! {PLAYER}! Are you okay?! Sit tight in there, okay? I'll go get help!\pYou here the footsteps of your sister exiting the gym.")
}

text(local) FlottTown_FlottCave_Text_Rescue{
    format("???: Ah! There you are! Come, let's get you out here.\p" 
            "ELSIE: {PLAYER}! Oh gods, I'm so glad you're okay!")
}

text(local) FlottTown_FlottCave_Text_LetsGoHome {
    format("BARNEY: Indeed it were. Now, how about we get you kids out of this dreadful place? You both look tired. Seeing as it's my gym, anything that happens here is on my head. My home is nearby; let's take a rest there.")
}