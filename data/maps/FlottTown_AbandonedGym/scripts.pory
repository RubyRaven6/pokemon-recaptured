raw`
.set LOCALID_ELSIE, 1
.set LOCALID_BALL, 2
`

mapscripts FlottTown_AbandonedGym_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_FLOTT_TOWN_STATE, 3: FlottTown_AbandonedGym_OnFrame
    ]
}

script FlottTown_AbandonedGym_OnFrame{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, FlottTown_AbandonedGym_WalkingIn)
    waitmovement(0)
    clearflag(FLAG_ELSIE_ENTERS)
    addobject(LOCALID_ELSIE)
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, FlottTown_AbandonedGym_FaceUp)
    applymovement(LOCALID_ELSIE, FlottTown_AbandonedGym_FaceUp)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    waitmovement(LOCALID_ELSIE)
    msgbox(format("ELSIE: Huh, this gym is pretty run down. I wonder what's inside?"), MSGBOX_DEFAULT)
    waitmessage
    closemessage
    msgbox(format("Elsie decided to follow you!"), MSGBOX_DEFAULT)
    waitmessage
    closemessage
    setfollower(LOCALID_ELSIE, FOLLOWER_ALL, FlottTown_AbandonedGym_ElsieFollowerScript, 0)
    setvar(VAR_FLOTT_TOWN_STATE, 4)
    end
}

script FlottTown_AbandonedGym_EventScript_FreePokeball{
    msgbox(format("It's a Pokéball! It seems there's no Pokémon inside it, though."), MSGBOX_AUTOCLOSE)
    removeobject(LOCALID_BALL, MAP_FLOTT_TOWN_ABANDONED_GYM)
    giveitem(ITEM_POKE_BALL, 1)
    setvar(VAR_FLOTT_TOWN_STATE, 5)
    closemessage
    msgbox(FlottTown_AbandonedGym_Text_BeforeCollapse, MSGBOX_DEFAULT)
    waitmessage
    closemessage
    end
}

script FlottTown_AbandonedGym_EventScript_GymCollapses{
    setmetatile(12, 17, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(13, 17, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(14, 17, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(12, 18, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(13, 18, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(14, 18, METATILE_Cave_CrackedFloor, FALSE)
    special(DrawWholeMapView)
    msgbox(format("ELSIE:{PLAYER}! Watch out!"))
    waitmessage
    closemessage
    setmetatile(12, 17, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(13, 17, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(14, 17, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(12, 18, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(13, 18, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(14, 18, METATILE_Cave_CrackedFloor_Hole, FALSE)
    special(DrawWholeMapView)
    setholewarp(MAP_FLOTT_TOWN_FLOTT_CAVE)
    setvar(VAR_FLOTT_TOWN_STATE, 6)
}

script FlottTown_AbandonedGym_ElsieFollowerScript{
    lock
    faceplayer
    if(var(VAR_FLOTT_TOWN_STATE) == 4){
        msgbox(format("ELSIE: I wonder what's inside? There must be something interesting deeper in."))
    }
    else{
        msgbox(format("ELSIE: What'cha waiting for? Let's get outta here!\p Once we get home, I can cook you up a hamburg steak."))
    }
    closemessage
    release
}

movement FlottTown_AbandonedGym_FaceUp{
    face_up
}

movement FlottTown_AbandonedGym_WalkingIn{
    walk_left
}

text FlottTown_AbandonedGym_Text_BeforeCollapse{
    format("ELSIE: Oh hey, a Pokéball! Maybe you can catch your own Pokémon with that someday?\p There doesn't seem to be anything else in here though. We'd best get home.")
}