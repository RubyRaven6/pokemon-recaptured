raw`
.set LOCALID_ELSIE, 1
.set LOCALID_BALL, 2
`

mapscripts FlottTown_AbandonedGym_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_FLOTT_TOWN_STATE, 2: FlottTown_AbandonedGym_BeforeEvent
        VAR_FLOTT_TOWN_STATE, 3: FlottTown_AbandonedGym_OnFrame
    ]
}

script FlottTown_AbandonedGym_BeforeEvent{
    setflag(FLAG_ELSIE_ENTERS_GYM)
    setflag(FLAG_VISITED_GYM)
    msgbox(format("This place seems pretty rundown. You should probably leave for now."), MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
    warp(MAP_FLOTT_TOWN, 9, 25)
    waitstate
}

script FlottTown_AbandonedGym_OnFrame{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, FlottTown_AbandonedGym_WalkingIn)
    waitmovement(0)
    clearflag(FLAG_ELSIE_ENTERS_GYM)
    addobject(LOCALID_ELSIE)
    applymovement(LOCALID_ELSIE, Common_Movement_FaceUp)
    lock
    waitmovement(LOCALID_ELSIE)
    msgbox(format("ELSIE: Huh, this gym is pretty run down. I wonder what's inside?"), MSGBOX_DEFAULT)
    msgbox(format("Elsie decided to follow you!"), MSGBOX_DEFAULT)
    waitmessage
    closemessage
    setfollower(LOCALID_ELSIE, FOLLOWER_ALL, FlottTown_AbandonedGym_ElsieFollowerScript, 0)
    setvar(VAR_FLOTT_TOWN_STATE, 4)
    end
}

script FlottTown_AbandonedGym_LadderStopper{
    msgbox(FlottTown_AbandonedGym_Text_LetsStayUp, MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, WalkAway)
}

script FlottTown_AbandonedGym_EventScript_FreePokeball{
    msgbox(format("It's a Pokéball! It seems there's no Pokémon inside it, though."), MSGBOX_AUTOCLOSE)
    removeobject(LOCALID_BALL, MAP_FLOTT_TOWN_ABANDONED_GYM)
    giveitem(ITEM_POKE_BALL, 1)
    setvar(VAR_FLOTT_TOWN_STATE, 5)
    closemessage
    msgbox(FlottTown_AbandonedGym_Text_BeforeCollapse, MSGBOX_NPC)
    waitmessage
    closemessage
    end
}

script FlottTown_AbandonedGym_EventScript_GymCollapses{
    applymovement(OBJ_EVENT_ID_FOLLOWER, Common_Movement_FacePlayer)
    setmetatile(12, 17, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(13, 17, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(14, 17, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(12, 18, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(13, 18, METATILE_Cave_CrackedFloor, FALSE)
    setmetatile(14, 18, METATILE_Cave_CrackedFloor, FALSE)
    special(DrawWholeMapView)
    applymovement(OBJ_EVENT_ID_FOLLOW_ME, Common_Movement_FacePlayer)
    msgbox(format("ELSIE: {PLAYER}! Watch out!"))
    setmetatile(12, 17, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(13, 17, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(14, 17, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(12, 18, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(13, 18, METATILE_Cave_CrackedFloor_Hole, FALSE)
    setmetatile(14, 18, METATILE_Cave_CrackedFloor_Hole, FALSE)
    warphole(MAP_FLOTT_TOWN_FLOTT_CAVE)
    special(DrawWholeMapView)
    setvar(VAR_FLOTT_TOWN_STATE, 6)
}

script FlottTown_AbandonedGym_ElsieFollowerScript{
    facefollower
    if(var(VAR_FLOTT_TOWN_STATE) == 4){
        msgbox(format("ELSIE: I wonder what's inside? There must be something interesting deeper in."))
        closemessage
    }
    else{
        msgbox(format("ELSIE: What'cha waiting for? Let's get outta here!\p Once we get home, I can cook you up a hamburg steak."))
        closemessage
    }
}

movement FlottTown_AbandonedGym_WalkingIn{
    walk_left
    face_up
    step_end
}

movement WalkAway{
    walk_left
    step_end
}

text FlottTown_AbandonedGym_Text_LetsStayUp {
    format("ELSIE: A ladder? Hmm...let's not go down there.")
}

text FlottTown_AbandonedGym_Text_BeforeCollapse{
    format("ELSIE: Huh, it's just a Pokéball. Will you ever use it, though?\p There doesn't seem to be anything else in here though. We'd best get home.")
}