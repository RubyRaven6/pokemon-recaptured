const LOCALID_ELSIE = 1

mapscripts FlottTown_PlayerHouse_1F_MapScripts {
    MAP_SCRIPT_ON_LOAD: FlottTown_PlayerHouse_1F_OnLoad
}

script FlottTown_PlayerHouse_1F_OnLoad{
    if(flag(FLAG_UNPACKED_ROOM)){
        setdynamicwarp(MAP_FLOTT_TOWN_PLAYER_HOUSE_2F, 5, 1)
    }
    else{
        setdynamicwarp(MAP_FLOTT_TOWN_PLAYER_HOUSE_2F_UNPACKED, 5, 1)
    }
    if(!flag(FLAG_VISITED_GYM)){
        setmetatile(8, 1, METATILE_PlayersHouse_WallBottom, TRUE)
        setmetatile(8, 2, METATILE_PlayersHouse_FloorWithWallShadow, FALSE)
        setmetatile(10, 1, METATILE_PlayersHouse_WallMovingBox_Top, TRUE)
        setmetatile(10, 2, METATILE_PlayersHouse_WallMovingBox_Bottom, FALSE)
        setmetatile(0, 3, METATILE_PlayersHouse_MovingBox_Closed, TRUE)
        setmetatile(0, 4, METATILE_PlayersHouse_MovingBox_Stacked, TRUE)
        setmetatile(0, 5, METATILE_PlayersHouse_MovingBoxPair_Top, TRUE)
        setmetatile(0, 6, METATILE_PlayersHouse_MovingBoxPair_Bottom, TRUE)
        setmetatile(1, 4, METATILE_PlayersHouse_MovingBox_StackNoShadow, TRUE)
        setmetatile(1, 5, METATILE_PlayersHouse_MovingBox_Stacked, TRUE)
        setmetatile(1, 6, METATILE_PlayersHouse_MovingBox_Open, TRUE)
        setmetatile(5, 4, METATILE_PlayersHouse_BlueRugCenter, FALSE)
        setmetatile(5, 5, METATILE_PlayersHouse_BlueRugCenter, FALSE)
        setmetatile(8, 4, METATILE_PlayersHouse_BlueRugCenter, FALSE)
        setmetatile(8, 5, METATILE_PlayersHouse_BlueRugCenter, FALSE)
    }
}

script FlottTown_PlayerHouse_1F_Elsie{
    faceplayer
    if(!flag(FLAG_VISITED_GYM)){
        speakername("Elsie")
        msgbox(FlottTown_PlayerHouse_1F_Text_CheckOurRoom, MSGBOX_NPC)
    }
    elif(flag(FLAG_VISITED_GYM) && var(VAR_FLOTT_TOWN_STATE) < 9){
        clearflag(FLAG_ELSIE_ENTERS_GYM)
        setvar(VAR_FLOTT_TOWN_STATE, 3)
        speakername("Elsie")
        msgbox(FlottTown_PlayerHouse_1F_Text_AnAbandonedGym, MSGBOX_DEFAULT)
        waitmessage
        closemessage
        warp(MAP_FLOTT_TOWN_ABANDONED_GYM, 13, 18)
    }
    elif(var(VAR_FLOTT_TOWN_STATE) == 10){
        call(FlottTown_PlayerHouse_1F_StartYourAdventure)
    }
    else{
        call(FlottTown_PlayerHouse_1F_NPCElsie)
    }
}

script FlottTown_PlayerHouse_1F_StartYourAdventure{
    lockall
    faceplayer
    speakername("Elsie")
    msgbox(FlottTown_PlayerHouse_1F_Text_ImSorryForWalkingOut,MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_ELSIE, Common_Movement_FaceAwayPlayer)
    waitmovement(0)
    delay(120)
    applymovement(LOCALID_ELSIE, Common_Movement_FacePlayer)
    waitmovement(0)
    speakername("Elsie")
    msgbox(FlottTown_PlayerHouse_1F_Text_CallingYouChamp,MSGBOX_DEFAULT)
    giveitem(ITEM_TOWN_MAP)
    setflag(FLAG_RECEIVED_RUNNING_SHOES)
    setflag(FLAG_SYS_B_DASH)
    playfanfare(MUS_OBTAIN_ITEM)
    speakername("Elsie")
    msgbox(format("Elsie gave the Running Shoes!"))
    waitfanfare
    speakername("Elsie")
    msgbox(FlottTown_PlayerHouse_1F_Text_ILoveYou,MSGBOX_DEFAULT)
    setvar(VAR_FLOTT_TOWN_STATE, 11)
    releaseall
}

script FlottTown_PlayerHouse_2F_Gamecube{
    msgbox(format("It's your sister's old Gamecube.\p You recall fond memories playing on it with her."), MSGBOX_SIGN)
    waitmessage
    closemessage
}

script FlottTown_PlayerHouse_2F_Sink{
    msgbox(format("It's a sink with a garbage disposal.\p You resist the urge to stick your hand in."), MSGBOX_SIGN)
    waitmessage
    closemessage
}

script FlottTown_PlayerHouse_2F_Cabinet{
    if(var(VAR_FLOTT_TOWN_STATE) < 4){
        msgbox(format("It's an empty dish cabinet."), MSGBOX_SIGN)
    }
    else{
        msgbox(format("It's a cabinet full of dishes."), MSGBOX_SIGN)
    }
    waitmessage
    closemessage
}

script FlottTown_PlayerHouse_2F_Fridge{
    msgbox(format("It's a brand new fridge!\p It's so brand new, there's nothing in it yet."), MSGBOX_SIGN)
    waitmessage
    closemessage
}

script FlottTown_PlayerHouse_1F_NPCElsie{
    lockall
    speakername("Elsie")
    msgbox("Is my Champ tired? Come rest for a while.", MSGBOX_DEFAULT)
    waitmessage
    closemessage
    playfanfare(MUS_HEAL)
    waitfanfare
    special(HealPlayerParty)
    speakername("Elsie")
    msgbox("There. Go get 'em, tiger.", MSGBOX_DEFAULT)
    waitmessage
    closemessage
    releaseall
    end
}

text FlottTown_PlayerHouse_1F_Text_AnAbandonedGym {
    format("Hey! I just got finished unpacking the boxes.\l"
            "How was the town?\p"
            "…\p"
            "An abanonded gym, huh? Are you sure you want to go in?\p"
            "…\p"
            "Well, it's better than you sneaking in yourself. Sure, I'll come with you."

    )
}

text FlottTown_PlayerHouse_1F_Text_CheckOurRoom{
    format("Pretty nice place, huh? Why not check our room out? I'll tidy some stuff up here.")
}

text FlottTown_PlayerHouse_1F_Text_ImSorryForWalkingOut{
    format("Oh, hey, {PLAYER}.\p"
            "…\p"
            "I'm… sorry for walking out. I hope Mr. Heinrich didn't think it was too rude.\p"
            "…\p" 
            "I'm not mad at you for having a Pokémon. Really, I'm not."
            "But… I saw that twinkle in your eye when Mr. Heinrich talked about training it. I'm sure he means taking on the gym challenge."
            "You know what's waiting at the end of that, right?\p"
            "Sooner or later, you'll have to challenge the Pokémon League… and go toe to toe with…\p"
            "With Dad.\p"
            "I've spent so long trying to get us away from him, and now you might go on a path that leads you straight to him."
    )
}

text FlottTown_PlayerHouse_1F_Text_CallingYouChamp{
    format("Hmph. Who am I kidding? I've been the one calling you Champ all of these years. It's on me for not seeing this coming someday."
            "Go get 'im, Champ. I'll always be here for you.\p"
            "In fact, here's a little something for the road."
        )
}

text FlottTown_PlayerHouse_1F_Text_ILoveYou{
    format(
        "Heh… just moved in and here you are flying out of the coop.\p"
        "I love you, {PLAYER}. Remember to stop by sometime, okay?"
        )
}