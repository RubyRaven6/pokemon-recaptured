mapscripts FlottTown_MapScripts{
}

// Flott town has different states depending on what's already happened. Uses VAR_FLOTT_TOWN
// 0 = Nothing; start of the game
// 1 = Exited ship cabin
// 2 = S.S. Anne Departs
// 3 = Elsie brings us to our new home
// 4 = Entered the Gym
// 5 = Took the Pokeball
// 6 = Fell Into a cave
// 7 = In the cave, after Elsie leavs to get help.

script FlottTown_EventScript_ElsieBringsUsHome1{
    applymovement(1, ElsieLooksAtDown)
    waitmovement(0)
    applymovement(1, Common_Movement_ExclamationMark)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveNextToElsie1)
    waitmovement(0)
    goto(FlottTown_EventScript_ElsieBringsUsHome4)
}

script FlottTown_EventScript_ElsieBringsUsHome2{
    applymovement(1, ElsieLooksAtDown)
    waitmovement(0)
    applymovement(1, Common_Movement_ExclamationMark)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveNextToElsie2)
    waitmovement(0)
    goto(FlottTown_EventScript_ElsieBringsUsHome4)
}

script FlottTown_EventScript_ElsieBringsUsHome3{
    applymovement(1, ElsieLooksAtDown)
    waitmovement(0)
    applymovement(1, Common_Movement_ExclamationMark)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveNextToElsie3)
    waitmovement(0)
    goto(FlottTown_EventScript_ElsieBringsUsHome4)
}

script FlottTown_EventScript_ElsieBringsUsHome4{
    applymovement(1, ElsieLooksAtPlayer)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerLooksAtElsie)
    waitmovement(1)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(FlottTown_Text_TookYourTime, MSGBOX_DEFAULT)
    waitmessage
    applymovement(1, LookAtTown)
    applymovement(OBJ_EVENT_ID_PLAYER, LookAtTown)
    waitmovement(1)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(FlottTown_Text_LookAtTown, MSGBOX_DEFAULT)
    closemessage
    delay(180)
    applymovement(1, ElsieLooksAtPlayer)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerLooksAtElsie)
    waitmovement(1)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(FlottTown_Text_LetsTakeALook, MSGBOX_DEFAULT)
    closemessage
    applymovement(1, ElsieWalksUp)
    waitmovement(0)
    applymovement(1, ElsieWalksToHouse)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerWalksToHouse)
    waitmovement(1)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    applymovement(1, LookAtTown)
    applymovement(OBJ_EVENT_ID_PLAYER, LookAtTown)
    msgbox(FlottTown_Text_OurNewHome, MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(1, ElsieLooksAtPlayer)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerLooksAtElsie)
    waitmovement(1)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(FlottTown_Text_DoYouLikeIt, MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        msgbox(FlottTown_Text_ImGladYouLikeIt, MSGBOX_DEFAULT)
        waitmessage
    }
    else{
        msgbox(FlottTown_Text_ImSorryChamp, MSGBOX_DEFAULT)
        waitmessage
    }
    msgbox(FlottTown_Text_IllPrepareInside,MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(1, ElsieGoesInside1)
    waitmovement(0)
    delay(16)
    opendoor(10, 6)
    waitdooranim
    applymovement(1, ElsieGoesInside2)
    waitmovement(0)
    closedoor(10, 6)
    waitdooranim
    setvar(VAR_FLOTT_TOWN_STATE, 3)
    end
}

script FlottTown_PlayerHouseSign{
    msgbox(FlottTown_Text_PlayerHouseSign, MSGBOX_SIGN)
}

script FlottTown_BarneyHouseSign{
    msgbox(FlottTown_Text_BarneyHouseSign, MSGBOX_SIGN)
}

script FlottTown_OldGymSign{
    msgbox(FlottTown_Text_OldGymSign, MSGBOX_SIGN)
}

movement LookAtTown {
    face_up    
}

movement MoveNextToElsie1{
    walk_up * 2
    walk_right
    walk_up
}

movement MoveNextToElsie2{
    walk_up * 3
}

movement MoveNextToElsie3{
    walk_up * 2
    walk_left
    walk_up
}

movement ElsieLooksAtDown{
    face_down
}

movement ElsieLooksAtPlayer{
    face_right
}

movement PlayerLooksAtElsie{
    face_left
}

movement ElsieWalksUp{
    walk_up
    walk_right
}

movement ElsieWalksToHouse{
    walk_right
    walk_up
    walk_up * 9
    walk_left
    walk_left * 18
}

movement PlayerWalksToHouse{
    walk_up
    walk_right
    walk_up * 9
    walk_up
    walk_left * 18 
}

movement ElsieGoesInside1{
    walk_up * 3
    walk_right
    face_up
}

movement ElsieGoesInside2{
    walk_up
	set_invisible
	step_end
}

text FlottTown_Text_TookYourTime{
    format("Hey! You really took your time, huh?")
}

text FlottTown_Text_LookAtTown{
    format("So, this is our new home. Beautiful, isn't it?")
}

text FlottTown_Text_LetsTakeALook{
    format("Well, that's enough sight-seeing. Let's go check our our new house!")
}

text FlottTown_Text_OurNewHome{
    format("This is it! This is where we'll be living from now on.")
}

text FlottTown_Text_DoYouLikeIt{
    format("So Champ, do you like the new place?")
}

text FlottTown_Text_ImGladYouLikeIt{
    format("Good to hear! Of course, anyone's gonna be glad we got out of that old hole. We don't have to suffer under his roof anymore. Plus, we got a killer view.")
}

text FlottTown_Text_ImSorryChamp{
    format("...\pI'm sorry, Champ. I know you miss all your friends back...well I don't even wanna call it home anymore. But, hey, at least we aren't suffering under his roof anymore, right? I promise, you'll see all of them again soon.")
}

text FlottTown_Text_IllPrepareInside{
    format("I'll get some stuff ready inside. Why not take a look around town?")
}

text FlottTown_Text_PlayerHouseSign{
    format("ASTER RESIDENCE")
}

text FlottTown_Text_BarneyHouseSign{
    format("HEINRICH RESIDNECE")
}

text FlottTown_Text_OldGymSign{
    format("The text is faded, but you can just barely make out the text written on it.\p FLOTT TOWN GYM\p LEADER: BARNEY HEINRICH\p The pursuant excavator!")
}